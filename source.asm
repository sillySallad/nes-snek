@0x00
VAR nmiflag
VAR player1
VAR headx
VAR heady
VAR headi
VAR taili
VAR movedir
VAR nextmovedir
VAR temp1
VAR movedelay
VAR movetimer
VAR tempx
VAR tempy
VAR uploadptr
VAR fooddelay
VAR foodtimer
VAR foodcount

PPUCTRL		= 0x2000
PPUMASK		= 0x2001
PPUSTATUS	= 0x2002
OAMADDR		= 0x2003
OAMDATA		= 0x2004
PPUSCROLL	= 0x2005
PPUADDR		= 0x2006
PPUDATA		= 0x2007
OAMDMA		= 0x4014
CONTROLLERSTROBE	= 0x4016
CONTROLLER1			= 0x4016

SPRITES	= 0x0200
UPLOAD	= 0x0300
BODYX	= 0x0400
BODYY	= 0x0500
BODYT	= 0x0600

ORIGIN = 0xC000

STRING: DATA "LLOHA\00"
DIROFFSET: DATA "\01\00\FF\00" DATA "\00\01\00\FF"
BUTTONTODIR: DATA "\03\01\02\00"
DIRMASK: DATA "\01\02\04\08"

==RESET==
SEI
CLD
LDA #0
STA nmiflag
LDA #10
STA movetimer
STA movedelay
LDA #35
STA foodtimer
STA fooddelay

; CLEAR BODY
LDX #0
LDA #0xFF
{
	STA BODYX,x
	STA BODYY,x
	STA BODYT,x
	DEX
	BNE RE
}

; READY PPU
LDX #4
{
	{
		BIT PPUSTATUS
		BPL RE
	}
	DEX
	BNE RE
}
LDA #0b11000000
STA PPUCTRL
LDA #0b00011110
STA PPUMASK

; LDA PPUSTATUS

; SETUP PALETTE
LDX #0x3F
STX PPUADDR
LDA #0
STA PPUADDR
; BLACK
LDA #0x1D
STA PPUDATA
; GREEN
LDA #0x0A
STA PPUDATA
LDA #0x19
STA PPUDATA
LDA #0x1A
STA PPUDATA
; RED
STX PPUADDR
LDA #0x11
STA PPUADDR
LDA #0x07
STA PPUDATA
LDA #0x06
STA PPUDATA
LDA #0x05
STA PPUDATA
; WHITE
LDA #0x1D
STA PPUDATA
LDA #0x1D
STA PPUDATA
LDA #0x30
STA PPUDATA

; INIT BODY
LDA #0x22
STA PPUADDR
LDA #0x10
STA PPUADDR
LDA #0x10
STA BODYY
STA BODYY+1
STA BODYY+2
LDY #0x10
STY BODYX
INY
STY BODYX+1
INY
STY BODYX+2
LDA #0b00000001
STA BODYT
STA PPUDATA
LDA #0b00000101
STA BODYT+1
STA PPUDATA
LDA #0b00001011
STA BODYT+2
STA PPUDATA
LDA #0
STA taili
LDA #2
STA headi

; CLEAR ATTRIBUTES (TODO)

; CLEAR SPRITES
LDX #0
LDA #0xFF
{
	STA SPRITES,x
	INX
	BNE RE
}

; EYE SPRITE
LDA #0x7F
STA SPRITES
LDA #0b00011011
STA SPRITES+1
LDA #0x01
STA SPRITES+2
LDA #0x90
STA SPRITES+3

; MAIN LOOP
{
	LDA #0x80
	STA nmiflag
	{
		BIT nmiflag
		BMI RE
	}
	
	; INPUT
	LDA #1
	STA CONTROLLERSTROBE
	LDA #0
	STA CONTROLLERSTROBE
	STA player1
	LDX #8
	{
		LDA CONTROLLER1
		LSR A
		ROL player1
		DEX
		BNE RE
	}
	LDA player1
	AND #0x0F
	BEQ SK
	{
		LDX #3
		{
			LSR A
			BCS BR
			DEX
			BNE RE
		}
		LDA BUTTONTODIR,x
		CMP movedir
		BEQ BR
		TAX
		LDA movedir
		CLC
		ADC #2
		AND #3
		STA temp1
		CPX temp1
		BEQ BR
		STX nextmovedir
	}
	
	; MOVE
	LDX movetimer
	DEX
	BEQ SK
	{
		JMP BR,SK
	}
	{
		LDA nextmovedir
		STA movedir
		
		; DO MOVE
		LDX movedir
		LDY headi
		LDA BODYX,y
		CLC
		ADC DIROFFSET,x
		AND #0x1F
		STA tempx
		LDA BODYY,y
		CLC
		ADC DIROFFSET+4,x
		CMP #0xFF
		BNE SK
		{
			LDA #0x1D
			BNE BR,SK
		}
		{
			CMP #0x1D
			BMI SK
			{
				LDA #0
			}
		}
		STA tempy
		LDA BODYT,y
		EOR #0x0F
		ORA DIRMASK,x
		STA BODYT,y
		
		; ADD NEW HEAD
		INY
		LDA tempx
		STA BODYX,y
		LDA tempy
		STA BODYY,y
		TXA
		CLC
		ADC #2
		AND #3
		TAX
		LDA DIRMASK,x
		EOR #0x0F
		STA BODYT,y
		STY headi
		
		LDX #0xFF
		JSR UPLOADSEGMENT
		
		DEY
		LDA BODYT,y
		JSR UPLOADSEGMENT
		
		LDA #0
		LDY taili
		JSR UPLOADSEGMENT
		
		; PATCH NEW TAIL
		LDA BODYT,y
		STA tempx
		ASL A
		ASL A
		AND #0x0C
		STA temp1
		LDA tempx
		LSR A
		LSR A
		AND #0x03
		ORA temp1
		EOR #0x0F
		INY
		AND BODYT,y
		STA BODYT,y
		
		JSR UPLOADSEGMENT
		STY taili
		STX uploadptr
		
		LDX movedelay
	}
	STX movetimer
	
	; CACHE HEAD AND WRITE EYES
	LDX headi
	LDA BODYT,x
	ORA #0x10
	STA SPRITES+1
	LDA BODYX,x
	ASL A
	ASL A
	ASL A
	STA headx
	STA SPRITES+3
	LDA BODYY,x
	ASL A
	ASL A
	ASL A
	TAX
	DEX
	STX heady
	STX SPRITES+0
	
	
	
	; FOOD CODE
	LDX foodtimer
	DEX
	BNE SK
	{
		LDY foodcount
		CPY #5
		BPL SK
		{
			
		}
		LDX fooddelay
	}
	STX foodtimer
	
	JMP RE
}

==UPLOADSEGMENT==
; TILE TO UPLOAD IN A
; SEGMENT INDEX IN Y
; OUTPUT TO UPLOAD,X
; (WILL CLOBBER temp1)
INX
STA UPLOAD,x
LDA BODYY,y
CLC
ROR A
ROR A
ROR A
STA temp1
ROR A
AND #0xF8
ORA BODYX,y
INX
STA UPLOAD,x
LDA temp1
AND #0x03
ORA #0x20
INX
STA UPLOAD,x
RTS

==BODYINTILE==


==IRQ==
RTI

==NMI==
BIT nmiflag
BMI SK
{
	RTI
}
PHA
TXA
PHA
TYA
PHA ; SAVE REGS

LDA PPUSTATUS

LDX uploadptr
BEQ SK
{
	LDA UPLOAD,x
	STA PPUADDR
	DEX
	LDA UPLOAD,x
	STA PPUADDR
	DEX
	LDA UPLOAD,x
	STA PPUDATA
	DEX
	BPL RE
}
STX uploadptr

LDA #0x02
STA OAMDMA

LDA #0
STA PPUSCROLL
STA PPUSCROLL
STA nmiflag

PLA
TAY
PLA
TAX
PLA ; LOAD REGS
RTI

@0xFFFA
DATA NMI
DATA RESET
DATA IRQ

